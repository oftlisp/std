(module std/internal/prelude/result
  either
  err
  err?
  must
  ok
  ok?
  result?
  result.pure
  result.unwrap
  result.unwrap-err)

(import std/internal/prelude/assert
  assert)
(import std/internal/prelude/logic
  and)
(import std/internal/prelude/objects/methods
  object-put-method)
(import std/internal/prelude/util
  shl?)
(import std/internal/prelude/util/macros
  definit
  unless)
(import std/internal/prelude/util/pseudoprims
  id)
(import std/internal/primitives
  car
  cdr
  cons
  nil?
  object?
  object-cons
  object-type
  object-value
  panic
  prim-eq)

(defn result? (r)
  (and (object? r)
       (prim-eq #result (object-type r))))

(defn ok (x ... xs)
  (def v (if (nil? xs) x (cons x xs)))
  (object-cons #result (cons 'ok v)))

(defn err (msg)
  (object-cons #result (cons 'err msg)))

(defn ok? (r) (and (result? r) (shl? (object-value r) 'ok)))
(defn err? (r) (and (result? r) (shl? (object-value r) 'err)))

(defn either (r if-ok if-err)
  (assert (result? r))
  ((if (err? r) if-err if-ok) (cdr (object-value r))))

(defn result.unwrap (r)
  (assert (ok? r))
  (object-value r))

(defn result.unwrap-err (r)
  (assert (err? r))
  (object-value r))

(defn must (this)
  (either this id panic))

(defn result.<$> (this f)
  (either this
    \(ok (f $))
    \this))

(defn result.<*> (this f)
  (either f
    \(result.<$> this $)
    \f))

(defn result.>>= (this f)
  (either this
    \(f $)
    \this))

(defn result.pure (x)
  (ok x))

(definit
  (object-put-method #result '<$> result.<$>)
  (object-put-method #result '<*> result.<*>)
  (object-put-method #result '>>= result.>>=)
  (object-put-method #result 'pure result.pure))
