;; Mathematical functions.
(module std/internal/prelude/math
  *
  +
  -
  /
  /=
  <
  <=
  >
  >=
  0?
  0-
  1+
  1-
  bitand
  bitor
  bitxor
  even?
  mod
  odd?)

(import std/internal/prelude/constants
  true)
(import std/internal/prelude/fp/list
  list.foldl)
(import std/internal/prelude/util/pseudoprims
  not)
(import std/internal/primitives
  */2
  +/2
  -/2
  <>
  =
  bitand/2
  bitor/2
  bitxor/2
  div/2
  mod/2
  prim-eq)

;; Multiplies numbers.
(defn * (n ... ns) (list.foldl ns */2 n))

;; Adds numbers.
(defn + (n ... ns) (list.foldl ns +/2 n))

;; Subtracts numbers.
(defn - (n ... ns) (list.foldl ns -/2 n))

;; Divides numbers.
(defn / (n ... ns) (list.foldl ns div/2 n))

(defn bitand (n ... ns) (list.foldl ns bitand/2 n))
(defn bitor (n ... ns) (list.foldl ns bitor/2 n))
(defn bitxor (n ... ns) (list.foldl ns bitxor/2 n))

;; Takes the modulus between numbers.
(defn mod (n ... ns) (list.foldl ns mod/2 n))

;; Numeric not-equal.
(defn /= (a b) (not (= a b)))

;; Checks if a value is numerically equal to zero.
(defn 0? (n) (= n 0))

;; Negates a number.
(defn 0- (n) (- 0 n))

;; Adds one to the given number.
(defn 1+ (n) (+ n 1))

;; Subtracts one from the given number.
(defn 1- (n) (- n 1))

(defn < (l r) (prim-eq (<> l r) '<))
(defn > (l r) (prim-eq (<> l r) '>))

(defn <= (l r)
  (def c (<> l r))
  (if (prim-eq c '<) true (prim-eq c '=)))
(defn >= (l r)
  (def c (<> l r))
  (if (prim-eq c '>) true (prim-eq c '=)))

(defn even? (x)
  (0? (mod x 2)))
(defn odd? (x)
  (not (even? x)))
