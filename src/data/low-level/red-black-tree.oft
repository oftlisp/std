;; This module implements a red-black tree based on the one presented in Chris
;; Okasaki's Purely Functional Data Structures, with the deletion algorithm
;; proposed by Matt Might.
;;
;; You probably don't want to import this module directly; rather, use
;; `std/data/map` or `std/data/set`.
;;
;; TODO: This needs a cleanup once pattern-matching is implemented.
;; TODO: This doesn't actually implement deletion yet.
(module std/data/low-level/red-black-tree
  list<-tree
  tree<-list
  tree-contains?
  tree-delete-many
  tree-empty
  tree-insert
  tree-insert-many)

; TREE = (COLOR TREE VALUE TREE)
;      | COLOR
; COLOR = 0 | 1 | -1 | 2
; Note: 0 = red, 1 = black, 2 = double black, -1 = negative black

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

(def red 0)
(def black 1)
(def negative-black -1)

(def E black)
(defn E? (tree) (eq tree E))
(defn T (c l x r) (list c l x r))
(def T? cons?)
(def T-color car)
(def T-left  cadr)
(def T-value caddr)
(def T-right cadddr)

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

(defn tree-balance (color l x r eq? lt?)
  ; TODO: This *heavily* needs pattern-matching.
  (cond
    (and (eq color black)
         (T? l)
         (eq (T-color l) red)
         (T? (T-left l))
         (eq (T-color (T-left l)) red))
      (T red
        (T black
          (T-left (T-left l))
          (T-value (T-left l))
          (T-right (T-left l)))
        (T-value l)
        (T black
          (T-right l)
          x
          r))
    (and (eq color black)
         (T? l)
         (eq (T-color l) red)
         (T? (T-right l))
         (eq (T-color (T-right l)) red))
      (T red
        (T black
          (T-left l)
          (T-value l)
          (T-left (T-right l)))
        (T-value (T-right l))
        (T black
          (T-right (T-right l))
          x
          r))
    (and (eq color black)
         (T? r)
         (eq (T-color r) red)
         (T? (T-left r))
         (eq (T-color (T-left r)) red))
      (T red
        (T black
          l
          x
          (T-left (T-left r)))
        (T-value (T-left r))
        (T black
          (T-right (T-left r))
          (T-value r)
          (T-right r)))
    (and (eq color black)
         (T? r)
         (eq (T-color r) red)
         (T? (T-right r))
         (eq (T-color (T-right r)) red))
      (T red
        (T black
          l
          x
          (T-left r))
        (T-value r)
        (T black
          (T-left (T-right r))
          (T-value (T-right r))
          (T-right (T-right r))))
    else
      (T color l x r)))

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

(def tree-empty E)

(defn tree-contains? (x tree eq? lt?)
  (if (E? tree)
    false
    (let (node-value (T-value tree))
      (cond
        (eq? node-value x)
          true
        (lt? node-value x)
          (tree-contains? x (T-left  tree) eq? lt?)
        else
          (tree-contains? x (T-right tree) eq? lt?)))))

(defn tree-max (tree)
  (cond
    (E? tree)
      (panic "No largest element in an empty tree")
    (E? (T-right tree))
      (T-value tree)
    else
      (tree-max (T-right tree))))

(defn tree-insert (x tree eq? lt?)
  (defn helper (tree)
    (if (E? tree)
      (T red E x E)
      ; TODO: Use pattern-matching destructuring.
      (let (color (T-color tree))
           (l     (T-left  tree))
           (y     (T-value tree))
           (r     (T-right tree))
        (cond
          (eq? x y) tree
          (lt? x y) (tree-balance color (helper l) y r eq? lt?)
          else      (tree-balance color l y (helper r) eq? lt?)))))
  (def tmp (helper tree))
  (cons black (cdr tmp)))

(defn tree-insert-many (l tree eq? lt?)
  (foldl l (fn (tree x) (tree-insert x tree eq? lt?)) tree))

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

(defn list<-tree (t)
  (if (T? t)
    (append
      (list<-tree (T-left t))
      (list (T-value t))
      (list<-tree (T-right t)))
    nil))

(defn tree<-list (l eq? lt?)
  (tree-insert-many l E eq? lt?))

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

(defn tree-delete-many (l tree eq? lt?)
  (defn contains-by? (x l)
    (cond
      (nil? l)       false
      (eq x (car l)) true
      else           (contains-by? x (cdr l))))
  (-> (list<-tree tree)
    (filter \(not (contains-by? $ l)))
    (tree<-list eq? lt?)))
