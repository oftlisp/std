(module std/internal/prelude/utils/pseudoprims
  append
  concat
  contains?
  id
  init
  length
  last
  list
  list?
  not
  reverse)

(import std/internal/prelude/constants
  false
  nil
  true)
(import std/internal/primitives
  +/2
  apply
  car
  cdr
  cons
  cons?
  eq
  nil?
  panic)

;; Reverses a list.
(defn reverse (l)
  (defn loop (acc l)
    (if (cons? l)
      (loop (cons (car l) acc) (cdr l))
      acc))
  (loop nil l))

;; Appends lists.
(defn append (... xs)
  (defn append/2 (x y)
    (defn helper (acc l)
      (if (cons? l)
        (helper (cons (car l) acc) (cdr l))
        acc))
    (helper y (reverse x)))
  (if (cons? xs)
    (append/2 (car xs) (apply append (cdr xs)))
    nil))

;; Concatenates a list of lists.
(defn concat (l)
  (apply append l))

;; Checks if a value is contained in a list.
(defn contains? (x l)
  (if (nil? l) false
    (if (eq (car l) x) true
      (contains? x (cdr l)))))

;; The identity function.
(defn id (x) x)

;; Returns the length of a list.
(defn length (l)
  (defn loop (l n)
    (if (nil? l)
      n
      (loop (cdr l) (+/2 n 1))))
  (loop l 0))

;; Constructs a list.
(defn list (... xs) xs)

;; Logical not.
(defn not (x)
  (if x false true))

;; Checks if a value is a list.
(defn list? (value)
  (if (cons? value)
    't
    (nil? value)))

;; Returns every element except for the last.
(defn init (l)
  (if (nil? l)
    (panic "Can't take init of nil")
    (if (nil? (cdr l))
      nil
      (cons (car l) (init (cdr l))))))

;; Returns the last element of the given list.
(defn last (l)
  (if (nil? l)
    (panic "Can't take last of nil")
    (if (nil? (cdr l))
      (car l)
      (last (cdr l)))))
