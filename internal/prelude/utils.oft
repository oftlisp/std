(module std/internal/prelude/utils
  ->
  todo)

(import std/internal/primitives
  car
  cdr
  cons?
  nil?)

;; The arrow macro of Clojure. Equivalents exist in ML, Elixir, and probably in
;; other languages.
;;
;; # Example
;;
;; ```
;; (-> (get-list)
;;   (map 1+)
;;   (filter (fn (x) (< x 0))))
;;
;; ; is equivalent to
;;
;; (filter (map (get-list) 1+) (fn (x) (< x 0)))
;; ```
(defmacro -> (fst ... rest)
  (defn helper (x xs)
    (if (nil? xs) x
      (progn
        (def hd (car xs))
        (def tl (cdr xs))
        (def next (if (cons? hd)
          `(,(car hd) ,x ,@(cdr hd))
          `(,hd ,x)))
        (helper next tl))))
  (helper fst rest))

;; Panics with the string "TODO".
(defmacro todo ()
  '(panic "TODO"))
