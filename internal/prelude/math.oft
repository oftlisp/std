;; Mathematical functions.
(module std/internal/prelude/math
  *
  +
  -
  /
  /=
  <
  <=
  >
  >=
  0?
  0-
  1+
  1-
  mod)

(import std/internal/prelude/constants
  true)
(import std/internal/prelude/fp
  foldl)
(import std/internal/prelude/util/pseudoprims
  not)
(import std/internal/primitives
  */2
  +/2
  -/2
  //2
  <>
  =
  eq
  mod/2)

;; Multiplies numbers.
(defn * (n ... ns) (foldl ns */2 n))

;; Adds numbers.
(defn + (n ... ns) (foldl ns +/2 n))

;; Subtracts numbers.
(defn - (n ... ns) (foldl ns -/2 n))

;; Divides numbers.
(defn / (n ... ns) (foldl ns //2 n))

;; Takes the modulus between numbers.
(defn mod (n ... ns) (foldl ns mod/2 n))

;; Numeric not-equal.
(defn /= (a b) (not (= a b)))

;; Checks if a value is numerically equal to zero.
(defn 0? (n) (= n 0))

;; Negates a number.
(defn 0- (n) (- 0 n))

;; Adds one to the given number.
(defn 1+ (n) (+ n 1))

;; Subtracts one from the given number.
(defn 1- (n) (- n 1))

(defn < (l r) (eq (<> l r) '<))
(defn > (l r) (eq (<> l r) '>))

(defn <= (l r)
  (def c (<> l r))
  (if (eq c '<) true (eq c '=)))
(defn >= (l r)
  (def c (<> l r))
  (if (eq c '>) true (eq c '=)))
