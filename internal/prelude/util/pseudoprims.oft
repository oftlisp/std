(module std/internal/prelude/util/pseudoprims
  append
  concat
  id
  init
  length
  last
  list
  list?
  not
  nth
  reverse
  vector)

(import std/internal/prelude/constants
  false
  nil
  true)
(import std/internal/primitives
  +/2
  -/2
  <>
  =
  apply
  car
  cdr
  cons
  cons?
  nil?
  object?
  object-type
  object-value
  panic
  prim-eq
  string?
  string.scalar-tail
  vector?
  vector<-list
  vector.length
  vector.nth)

;; Reverses a list.
(defn reverse (l)
  (defn loop (acc l)
    (if (cons? l)
      (loop (cons (car l) acc) (cdr l))
      acc))
  (loop nil l))

;; Appends lists.
(defn append (... xs)
  (defn append/2 (x y)
    (defn helper (acc l)
      (if (cons? l)
        (helper (cons (car l) acc) (cdr l))
        acc))
    (helper y (reverse x)))
  (if (cons? xs)
    (append/2 (car xs) (apply append (cdr xs)))
    nil))

;; Concatenates a list of lists.
(defn concat (l)
  (apply append l))

;; The identity function.
(defn id (x) x)

(defn list.length (l)
  (defn loop (l n)
    (if (nil? l)
      n
      (loop (cdr l) (+/2 n 1))))
  (loop l 0))

(defn string.length (s)
  (defn helper (s n)
    (if (prim-eq s "")
      n
      (helper (string.scalar-tail s) (+/2 n 1))))
  (helper s 0))

;; Returns the length of a list, string, or vector.
(defn length (l)
  (if (string? l)
    (string.length l)
    (if (vector? l)
      (vector.length l)
      (list.length l))))

;; Constructs a list.
(defn list (... xs) xs)

;; Constructs a vector.
(defn vector (... xs) (vector<-list xs))

;; Logical not.
(defn not (x)
  (if x false true))

;; Checks if a value is a list.
(defn list? (value)
  (if (cons? value)
    't
    (nil? value)))

;; Returns every element except for the last.
(defn init (l)
  (if (nil? l)
    (panic "Can't take init of nil")
    (if (nil? (cdr l))
      nil
      (cons (car l) (init (cdr l))))))

;; Returns the last element of the given list.
(defn last (l)
  (if (nil? l)
    (panic "Can't take last of nil")
    (if (nil? (cdr l))
      (car l)
      (last (cdr l)))))

;; Gets the nth value from a list.
(defn list.nth (l n)
  (defn loop (l n)
    (if (= n 0)
      (car l)
      (loop (cdr l) (-/2 n 1))))
  (if (prim-eq (<> n 0) '<)
    (panic "Can't get the list value at a negative index")
    (loop l n)))

;; Gets the nth value from a list or vector.
(defn nth (l n)
  (if (vector? l)
    (vector.nth l n)
    (list.nth l n)))
