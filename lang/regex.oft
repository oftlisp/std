;; A parser for regular expressions.
;;
;; Regular expressions are of the form:
;;
;;   REGEX ::= AND-BRANCHES
;;          |  "!" AND-BRANCHES
;;   AND-BRANCHES ::= AND-BRANCH
;;                 |  AND-BRANCH "&" AND-BRANCHES
;;   AND-BRANCH ::= OR-BRANCH
;;               |  OR-BRANCH "|" AND-BRANCH
;;   OR-BRANCH ::= PIECE*
;;   PIECE ::= ATOM QUALIFIER
;;   QUALIFIER ::= EMPTY | "+" | "*" | "?"
;;   ATOM ::= LITERAL-CHAR
;;         |  "\" SPECIAL-CHAR
;;         |  CHAR-SET
;;         |  "."
;;         |  "(" REGEX ")"
;;   LITERAL-CHAR ::= any character not in SPECIAL-CHAR
;;   SPECIAL-CHAR ::= "|" | "+" | "*" | "?" | "." | "("
;;                 |  ")" | "[" | "]" | "&" | "^" | "!"
;;   CHAR-SET ::= "[" CHAR-SET-GROUPS "]"
;;             |  "[" "^" CHAR-SET-GROUPS "]"
;;   CHAR-SET-GROUPS ::= EMPTY
;;                    | CHAR-SET-GROUP CHAR-SET-GROUPS
;;   CHAR-SET-GROUP ::= CHAR-SET-CHAR
;;                   |  CHAR-SET-CHAR "-" CHAR-SET-CHAR
;;   CHAR-SET-CHAR ::= CHAR-SET-LITERAL
;;                  |  "\" CHAR-SET-SPECIAL
;;   CHAR-SET-LITERAL ::= any character not in CHAR-SET-SPECIAL
;;   CHAR-SET-SPECIAL ::= SPECIAL-CHAR | "-"
;;
;; A regex AST looks like:
;;
;;   AST ::= CHAR-STRING
;;        |  any-char | empty | none
;;        |  (and AST...)
;;        |  (char-set CHAR-STRING CHAR-STRING)
;;        |  (concat AST...)
;;        |  (kleene AST)
;;        |  (not AST)
;;        |  (or AST...)
(module std/lang/regex
  format-regex
  parse-regex
  simplify-regex)

(import std/lang/regex/parser
  parse-cst-regex)

;; Simplifies a regex to an equivalent form.
(defn simplify-regex (regex)
  (cond
    (or (string? regex)
        (eq regex 'any-char)
        (eq regex 'empty)
        (eq regex 'none)
        (shl? regex 'char-set))
      regex
    (shl? regex 'and)
      (let1 asts (map (cdr regex) simplify-regex)
        (cond
          (nil? asts)
            '(not none)
          (nil? (cdr asts))
            (car asts)
          else
            (cons 'and asts)))
    (shl? regex 'concat)
      (let1 asts (-> (cdr regex)
                     (map simplify-regex)
                     (filter \(not (eq $ 'empty))))
        (cond
          (nil? asts)
            'empty
          (nil? (cdr asts))
            (car asts)
          (contains? asts 'none)
            'none
          else
            (cons 'concat asts)))
    (shl? regex 'kleene)
      (let1 ast (simplify-regex (cadr regex))
        (if (shl? ast 'kleene)
          (cadr ast)
          (list 'kleene ast)))
    (shl? regex 'not)
      (let1 ast (simplify-regex (cadr regex))
        (if (shl? ast 'not)
          (cadr ast)
          (list 'not ast)))
    (shl? regex 'or)
      (let1 asts (-> (cdr regex)
                     (map simplify-regex)
                     (filter \(not (eq $ 'none))))
        (cond
          (nil? asts)
            'none
          (nil? (cdr asts))
            (car asts)
          else
            (cons 'or asts)))
    else
      (panic "Invalid regex: " regex)))

;; Converts a regex AST to a string.
(defn format-regex (regex)
  (<- regex simplify-regex)
  (todo "format-regex"))

;; Parses a regex's string version, or one of its components, to an AST. If the
;; regex is valid, returns a cons whose head is the symbol `ok` and whose tail
;; is the AST. If the regex is not valid, returns a cons whose head is the
;; symbol `err` and whose tail is the error (which may be nil).
(defn parse-regex (regex)
  (defnrec
    (ast<-and-branch (cst)
      (if (nil? cst)
        'empty
        (cons 'or (map cst ast<-or-branch))))
    (ast<-and-branches (cst)
      (if (nil? cst)
        'empty
        (cons 'and (map cst ast<-and-branch))))
    (ast<-atom (cst)
      (cond
        (eq cst 'any-char) 'any-char
        (string? cst)      cst
        (nil? cst)         'empty
        (shl? cst 'regex)  (ast<-regex (cadr cst))
        (shl? cst 'just)   (cons 'or (map (cadr cst) ast<-char-set-group))
        (shl? cst 'not)    (list 'not (ast<-atom (cons 'just (cdr cst))))
        else               (panic "Unknown atom: " cst)))
    (ast<-or-branch (cst)
      (cons 'concat (map cst ast<-piece)))
    (ast<-char-set-group (cst)
      (cond
        (shl? cst 'range) (list 'char-set (cadr cst) (caddr cst))
        (shl? cst 'one)   (cadr cst)
        else              (panic "Unknown char-set-group: " cst)))
    (ast<-piece (cst)
      ((ast<-qualifier (cdr cst)) (ast<-atom (car cst))))
    (ast<-qualifier (cst)
      (case cst
        '?  \`(or ,$ empty)
        '*  \`(kleene ,$)
        '+  \`(concat ,$ (kleene ,$))
        nil id
            (panic "Unknown qualifier: " cst)))
    (ast<-regex (cst)
      (def inner (ast<-and-branches (cadr cst)))
      (case (car cst)
        'just inner
        'not  (list 'not inner)
              (panic "Unknown regex: " cst))))
  (>>= (parse-cst-regex regex)
    \(if (eq (cadr $) "")
      (-> (car $)
        ast<-regex
        simplify-regex
        ok)
      (err (concat-strings "Unparsed input: " (show (cadr $)))))))
